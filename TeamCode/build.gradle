//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // Kotlin v1.6.0
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.0'
    }
}

apply plugin: 'kotlin-android'

def getCommit = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

def getBranch = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

def getBuildTime = { ->
    return new Date().format("yyyy-MM-dd@HH:mm:ss", TimeZone.getDefault())
}

apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'

android {
    namespace = 'org.firstinspires.ftc.teamcode'
    packagingOptions {
        jniLibs.useLegacyPackaging true
    }

    defaultConfig {
        buildConfigField "String", "GIT_COMMIT", '"' + getCommit() + '"'
        buildConfigField "String", "GIT_BRANCH", '"' + getBranch() + '"'
        buildConfigField "String", "BUILD_TIME", '"' + getBuildTime() + '"'
    }
}

dependencies {
    implementation project(':FtcRobotController')
    annotationProcessor files('lib/OpModeAnnotationProcessor.jar')

    // RoadRunner
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7'
    implementation 'com.acmerobotics.roadrunner:core:0.5.6'
}
